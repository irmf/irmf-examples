/*{
  "author": "Glenn M. Lewis",
  "license": "Apache-2.0",
  "date": "2020-02-14",
  "irmf": "1.0",
  "language": "wgsl",
  "materials": ["PLA"],
  "max": [3,3,0.1],
  "min": [-3,-3,-0.1],
  "notes": "This is a 2D line that can be extruded.",
  "options": {},
  "title": "line2d with start and end points",
  "units": "mm",
  "version": "1.0"
}*/

fn line2d_len(lineLength: f32, width: f32, startRoundCap: bool, endRoundCap: bool, xyz: vec3f) -> f32 {
  if (xyz.x < 0.0) {
    if (startRoundCap) {
      let r = length(xyz.xy);
      if (r > 0.5 * width) { return 0.0; }
    } else { return 0.0; }
  }

  if (xyz.x > lineLength) {
    if (endRoundCap) {
      let r = length(xyz.xy - vec2f(lineLength, 0.0));
      if (r > 0.5 * width) { return 0.0; }
    } else { return 0.0; }
  }

  let r = abs(xyz.y);
  if (r > 0.5 * width) { return 0.0; }

  return 1.0;
}

fn line2d_pts(start: vec2f, end: vec2f, width: f32, startRoundCap: bool, endRoundCap: bool, xyz_orig: vec3f) -> f32 {
  var xyz = xyz_orig;
  let xy = xyz.xy - start;
  let diff = end - start;
  let lineLength = length(diff);
  let angle = atan2(diff.y, diff.x);
  let c = cos(angle);
  let s = sin(angle);
  let x = xy.x * c + xy.y * s;
  let y = xy.x * -s + xy.y * c;
  xyz.x = x;
  xyz.y = y;
  return line2d_len(lineLength, width, startRoundCap, endRoundCap, xyz);
}

fn mainModel4(xyz: vec3f) -> vec4f {
  var materials = vec4f(0.0);
  materials[0] = line2d_pts(vec2f(-1.0, -1.0), vec2f(1.0, 1.0), 0.5, true, true, xyz);
  return materials;
}
