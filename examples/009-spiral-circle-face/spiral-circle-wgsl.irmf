/*{
  "author": "Glenn M. Lewis",
  "license": "Apache-2.0",
  "date": "2020-02-13",
  "irmf": "1.0",
  "language": "wgsl",
  "materials": ["PLA"],
  "max": [5.5,5.5,0.5],
  "min": [-5.5,-5.5,-0.5],
  "notes": "Simple IRMF shader - spiral with circle cross-section face.",
  "options": {},
  "title": "spiral",
  "units": "mm",
  "version": "1.0"
}*/

const M_PI = 3.1415926535897932384626433832795;

fn spiralCircleFace(startRadius: f32, size: f32, gap: f32, nTurns: f32, xyz: vec3f) -> f32 {
  // First, trivial reject above and below the spiral.
  if (xyz.z < -0.5 * size || xyz.z > 0.5 * size) { return 0.0; }
  
  let r = length(xyz.xy);
  if (r < startRadius - 0.5 * size ||
      r > startRadius + 0.5 * size + (size + gap) * nTurns) { return 0.0; }
  
  // If the current point is between the spirals, return no material:
  var angle = atan2(xyz.y, xyz.x) / (2.0 * M_PI);
  if (angle < 0.0) { angle += 1.0; } // 0 <= angle <= 1 between spirals from center to center.
  // 0 <= dr <= (size+gap) between spirals from center to center.
  let dr = (r - startRadius) - (size + gap) * floor((r - startRadius) / (size + gap));
  
  var coilNum = 0.0;
  var lastSpiralR_tmp = angle * (size + gap);
  if (lastSpiralR_tmp > dr) {
    lastSpiralR_tmp -= (size + gap);  // center of current coil.
    coilNum = -1.0;
  }
  var nextSpiralR_tmp = lastSpiralR_tmp + (size + gap);  // center of next outer coil.
  
  // If the current point is within the gap between the two coils, reject it.
  if (dr > lastSpiralR_tmp + 0.5 * size && dr < nextSpiralR_tmp - 0.5 * size) { return 0.0; }
  
  coilNum += floor((r - startRadius + (0.5 * size) - lastSpiralR_tmp) / (size + gap));

  // If the current point is in a coil numbered outside the current range, reject it.
  if (coilNum < 0.0 || coilNum >= nTurns) { return 0.0; }

    // At this point, we are within the circle cross-section face, so let's round the edge.
  var lastSpiralR = floor((r - startRadius) / (size + gap)) * (size + gap) +
    startRadius + angle * (size + gap);
  if (lastSpiralR > r) { lastSpiralR -= (size + gap); }
  let nextSpiralR = lastSpiralR + size + gap;
  let rad_angle = angle * 2.0 * M_PI;
  let lastSpiralCenter = vec3f(lastSpiralR * cos(rad_angle), lastSpiralR * sin(rad_angle), 0.0);
  let nextSpiralCenter = vec3f(nextSpiralR * cos(rad_angle), nextSpiralR * sin(rad_angle), 0.0);
  let d1 = length(xyz - lastSpiralCenter);
  let d2 = length(xyz - nextSpiralCenter);
  if (d1 > 0.5 * size && d2 > 0.5 * size) { return 0.0; }
  
  return 1.0;
}

fn mainModel4(xyz: vec3f) -> vec4f {
  var materials = vec4f(0.0);
  materials[0] = spiralCircleFace(3.0, 0.85, 0.15, 2.0, xyz);
  return materials;
}
