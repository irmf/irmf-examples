/*{
  "author": "Glenn M. Lewis",
  "license": "Apache-2.0",
  "date": "2020-04-14",
  "irmf": "1.0",
  "language": "glsl",
  "materials": ["Red","Green"],
  "max": [5,5,5],
  "min": [-5,-5,-5],
  "notes": "IRMF logo model 1 with cubed pattern.",
  "options": {},
  "title": "Constructive Solid Geometry example",
  "units": "mm",
  "version": "1.0"
}*/

float sphere(in float radius, in vec3 xyz) {
  float r = length(xyz);
  return r <= radius ? 1.0 : 0.0;
}

float cyl(in float radius, in vec2 uv) {
  float r = length(uv);
  return r <= radius ? 1.0 : 0.0;
}

vec2 cubed(in float value, in vec3 xyz) {
  vec3 v = step(0.5, mod(xyz, 1.0));
  int count = int(v.x + v.y + v.z);
  return count == 0 || count == 2 ? vec2(value, 0) : vec2(0, value);
}

void mainModel4(out vec4 materials, in vec3 xyz) {
  const float radius = 5.6;
  const float r2 = 3.3;
  float value = 1.0 - sphere(radius, xyz) - cyl(r2, xyz.yz) - cyl(r2, xyz.xz) - cyl(r2, xyz.xy);
  materials.xy = cubed(value, xyz - vec3(0.6));
}
