/*{
  "author": "Glenn M. Lewis",
  "license": "Apache-2.0",
  "date": "2020-04-13",
  "irmf": "1.0",
  "language": "wgsl",
  "materials": ["Red","Green"],
  "max": [5,5,5],
  "min": [-5,-5,-5],
  "notes": "Figure 10 from: https://authors.library.caltech.edu/9756.",
  "options": {
    "resolution": 2048,
    "color1": [255,0,0,1],
    "color2": [0,255,0,1]
  },
  "title": "Superquadric toroids",
  "units": "mm",
  "version": "1.0"
}*/

fn wgsl_mod(x: f32, y: f32) -> f32 {
  return x - y * floor(x / y);
}

fn superquad2(slices: f32, e1: f32, e2: f32, a4: f32, xyz_orig: vec3f) -> vec2f {
  var xyz = xyz_orig;
  xyz *= 2.5;
  let result = select(vec2f(0.0, 1.0), vec2f(1.0, 0.0), wgsl_mod(abs(xyz.z) * 6.0, 1.0) <= 0.5);
  let angle = -1.3;
  let c = cos(angle);
  let s = sin(angle);
  let y_new = xyz.y * c - xyz.z * s;
  let z_new = xyz.y * s + xyz.z * c;
  xyz.y = y_new;
  xyz.z = z_new;
  let xyz_abs = abs(xyz);
  let f = pow(pow(pow(xyz_abs.x, 2.0 / e2) + pow(xyz_abs.y, 2.0 / e2), e2 / 2.0) - a4, 2.0 / e1) + pow(xyz_abs.z, 2.0 / e1);
  return select(vec2f(0.0), result, f <= 1.0);
}

fn mainModel4(xyz: vec3f) -> vec4f {
  var mat_xy = vec2f(0.0);
  let e = vec4f(0.1, 0.3, 1.0, 3.0);
  let o = vec4f(-3.4, - 1.2, 1.2, 3.4);
  for(var i = 0; i < 4; i ++ ) {
    for(var j = 0; j < 4; j ++ ) {
      mat_xy += superquad2(6.0, e[i], e[j], 1.5, xyz - vec3f(o[j], 0.0, o[3 - i]));
    }
  }
  return vec4f(mat_xy, 0.0, 0.0);
}
