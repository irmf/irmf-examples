/*{
  "author": "Glenn M. Lewis",
  "license": "Apache-2.0",
  "date": "2020-04-12",
  "irmf": "1.0",
  "language": "glsl",
  "materials": ["PLA"],
  "max": [5,5,5],
  "min": [-5,-5,-5],
  "notes": "Sphericon using two half superquadrics.",
  "options": {},
  "title": "Sphericon",
  "units": "mm",
  "version": "1.0"
}*/

float superquad(in float e1, in float e2, in vec3 xyz) {
  xyz = abs(xyz); // Due to GLSL 'pow' definition.
  float f = pow(pow(xyz.x, 2.0 / e2) + pow(xyz.y, 2.0 / e2), e2 / e1) + pow(xyz.z, 2.0 / e1);
  return f <= 1.0 ? 1.0 : 0.0;
}

float sphericon(in vec3 xyz) {
  if (xyz.x <= 0.0) {
    return superquad(2.0, 1.0, xyz);
  }
  return superquad(2.0, 1.0, xyz.xzy);
}

void mainModel4(out vec4 materials, in vec3 xyz) {
  xyz /= 5.0;
  materials[0] = sphericon(xyz);
}
